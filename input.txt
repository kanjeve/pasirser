#define LevelMax 5 

extern Proc1$ 
Proc1 =-1$ 
/* random poynomial generator */ 
def monic_randpoly_ff(N,V) {
  for ( I = 0, N1 = N-1, S = 0; I < N1; I++ )
    S += random_ff()*V^I; 
  return V^N1+S;
}

def c_z(F,E,Level) {
  V = var(F);
  N = deg(F,V);
  if ( N == E )
    return [F];
  M = field_order_ff();
  K = idiv(N,E);
  L = [F];
  while ( 1 ) {
    W = monic_randpoly_ff(2*E,V);
    T = generic_pwrmod_ff(W,F,idiv(M^E-1,2));
    W = T-1;
    if ( !W )
      continue;
    G = ugcd(F,W);
    if ( deg(G,V) && deg(G,V) < N ) {
      if ( Level >= LevelMax ) {
        L1 = c_z(G,E,Level+1);
        L2 = c_z(sdiv(F,G),E,Level+1);
      } else { 
        if ( Proc1 < 0 )
          Proc1 = ox_launch();
        ox_cmo_rpc(Proc1,"ox_c_z",lmptop(G),E,setmod_ff(),Level+1);
        L2 = c_z(sdiv(F,G),E,Level+1);
        L1 = ox_pop_cmo(Proc1);
        L1 = map(simp_ff,L1);
      } 
      return append(L1,L2);
    }
  }
}

end$